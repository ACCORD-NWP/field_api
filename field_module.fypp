! Rank and shape definitions for simple templating
!
! Note that the ranks encode coneptual dimensions here, eg. FIELD_2D encodes
! a surface field and FIELD_3D represents a field with a vertical component.
#:include "field_definition.fypp"

MODULE FIELD_MODULE
  ! The FIELD types provided by this module provide data abstractions that
  ! decouple data storage in memory from the data views used in thread-parallel
  ! sections of the code. They are intended to thinly wrap ATLAS_FIELD
  ! objects and provide additional features that may later be
  ! incorporated into Atlas. They can also provide backward-compatibility
  ! for non-Atlas execution modes.

USE PARKIND1, ONLY: JPIM, JPRB
USE OML_MOD, ONLY: OML_MAX_THREADS, OML_MY_THREAD
USE IEEE_ARITHMETIC, ONLY: IEEE_SIGNALING_NAN

IMPLICIT NONE

#:for dtype, suffix, rank, default in field_descriptors
#:set field_type_name = 'FIELD_%s%sD' % (suffix, rank)
TYPE :: ${field_type_name}$
  ! A FIELD encapsulates a single multi-dimensional array and can
  ! provide block-indexed "views" of the data for automating the
  ! allocation and parallel iterration of NPROMA blocks.

  ! TODO: Atlas-based field data storage field
  ! TODO: Do we still need to use pointers here?
  ! TYPE(ATLAS_FIELD), POINTER :: DATA

  ! Storage pointer for non-Atlas backward-compatibility mode
  !
  ! The underlying storage pointer has the rank as the dimension,
  ! where the innermost dimension represents the horizontal and
  ! the outermost one is the block index.
  ${dtype}$, POINTER :: PTR(${shapes[rank]}$) => NULL()

  ! A separate data pointer that can be used to create
  ! a contiguous chunk of host memory to cleanly map to
  ! device, should the %DATA pointer be discontiguous.
  ${dtype}$, POINTER, CONTIGUOUS :: DEVPTR(${shapes[rank]}$) => NULL()

  ! Number of blocks used in the data layout
  INTEGER :: NBLOCKS

  ! Flag indicating whether this field stores real data
  LOGICAL :: ACTIVE = .FALSE.
  ! Flag indicating the use a single block-buffer per thread
  LOGICAL :: THREAD_BUFFER = .FALSE.
  ! Flag indicating whether we own the allocated base array

  INTEGER(KIND=JPIM) :: LBOUNDS (${rank}$) = [${", ".join (["1"] * rank)}$]
  INTEGER(KIND=JPIM) :: ISTATUS = 0

CONTAINS

  PROCEDURE :: FINAL => ${field_type_name}$_FINAL
  PROCEDURE :: GET_VIEW => ${field_type_name}$_GET_VIEW

  ! GPU-specific device data transfer API
  PROCEDURE :: CREATE_DEVICE => ${field_type_name}$_CREATE_DEVICE
END TYPE ${field_type_name}$

#:for dm in data_management_type
TYPE, EXTENDS(${field_type_name}$) :: ${field_type_name}$_${dm}$
CONTAINS
  PROCEDURE :: FINAL => ${field_type_name}$_${dm}$_FINAL

  ! GPU-specific device data transfer API
  PROCEDURE :: UPDATE_DEVICE => ${field_type_name}$_${dm}$_UPDATE_DEVICE
  PROCEDURE :: UPDATE_HOST => ${field_type_name}$_${dm}$_UPDATE_HOST
  PROCEDURE :: DELETE_DEVICE => ${field_type_name}$_${dm}$_DELETE_DEVICE
END TYPE ${field_type_name}$_${dm}$

#:endfor

#:endfor

INTEGER (KIND=JPIM), PARAMETER :: NDEVFRESH = B'00000001', NHSTFRESH = B'00000010'
INTEGER (KIND=JPIM), PARAMETER, PRIVATE :: NH2D = 1, ND2H = 2, NRD = B'00000001', NWR = B'00000010'

#:for rank in ranks
TYPE FIELD_${rank}$D_PTR
  ! Struct to hold references to field objects
  TYPE(FIELD_${rank}$D), POINTER :: PTR => NULL()
END TYPE FIELD_${rank}$D_PTR

TYPE FIELD_${rank}$D_VIEW
  ! Struct to hold array views, so we can make arrays of them
  REAL(KIND=JPRB), POINTER :: P(${shapes[rank-1]}$) => NULL()
END TYPE FIELD_${rank}$D_VIEW

#:endfor

#:for dtype, suffix, rank, default in field_descriptors
#:set field_type_name = 'FIELD_%s%sD' % (suffix, rank)
INTERFACE ${field_type_name}$
  MODULE PROCEDURE :: ${field_type_name}$_WRAP
  MODULE PROCEDURE :: ${field_type_name}$_WRAP_PACKED
  ! MODULE PROCEDURE :: ${field_type_name}$_EMPTY
  MODULE PROCEDURE :: ${field_type_name}$_ALLOCATE
END INTERFACE

#:endfor

INTERFACE ALLOCATE_FIELD
#:for dtype, suffix, rank, default in field_descriptors
#:set field_type_name = 'FIELD_%s%sD' % (suffix, rank)
  MODULE PROCEDURE :: ALLOCATE_${field_type_name}$
#:endfor
END INTERFACE


INTERFACE FILL_BUFFER
#:for suffix in suffixes
  MODULE PROCEDURE :: FILL_BUFFER_${suffix}$2D, FILL_BUFFER_${suffix}$3D
  MODULE PROCEDURE :: FILL_BUFFER_${suffix}$4D, FILL_BUFFER_${suffix}$5D
#:endfor
END INTERFACE FILL_BUFFER


INTERFACE FIELD_CREATE_DEVICE
#:for dtype, suffix, rank, default in field_descriptors
#:set field_type_name = 'FIELD_%s%sD' % (suffix, rank)
  MODULE PROCEDURE :: ${field_type_name}$_CREATE_DEVICE
#:endfor
END INTERFACE FIELD_CREATE_DEVICE

INTERFACE FIELD_UPDATE_DEVICE
#:for dtype, suffix, rank, default in field_descriptors
#:set field_type_name = 'FIELD_%s%sD' % (suffix, rank)
  MODULE PROCEDURE :: ${field_type_name}$_WRAPPER_UPDATE_DEVICE
  MODULE PROCEDURE :: ${field_type_name}$_OWNER_UPDATE_DEVICE
#:endfor
END INTERFACE FIELD_UPDATE_DEVICE

INTERFACE FIELD_UPDATE_HOST
#:for dtype, suffix, rank, default in field_descriptors
#:set field_type_name = 'FIELD_%s%sD' % (suffix, rank)
  MODULE PROCEDURE :: ${field_type_name}$_WRAPPER_UPDATE_HOST
  MODULE PROCEDURE :: ${field_type_name}$_OWNER_UPDATE_HOST
#:endfor
END INTERFACE FIELD_UPDATE_HOST

INTERFACE FIELD_DELETE_DEVICE
#:for dtype, suffix, rank, default in field_descriptors
#:set field_type_name = 'FIELD_%s%sD' % (suffix, rank)
  MODULE PROCEDURE :: ${field_type_name}$_WRAPPER_DELETE_DEVICE
  MODULE PROCEDURE :: ${field_type_name}$_OWNER_DELETE_DEVICE
#:endfor
END INTERFACE FIELD_DELETE_DEVICE

INTERFACE GET_DEVICE_DATA
#:for dtype, suffix, rank, default in field_descriptors
#:set field_type_name = 'FIELD_%s%sD' % (suffix, rank)
  MODULE PROCEDURE :: ${field_type_name}$_WRAPPER_GET_DEVICE_DATA
  MODULE PROCEDURE :: ${field_type_name}$_OWNER_GET_DEVICE_DATA
#:endfor
END INTERFACE GET_DEVICE_DATA

INTERFACE GET_DEVICE_DATA_RDONLY
#:for dtype, suffix, rank, default in field_descriptors
#:set field_type_name = 'FIELD_%s%sD' % (suffix, rank)
  MODULE PROCEDURE :: ${field_type_name}$_GET_DEVICE_DATA_RDONLY
#:endfor
END INTERFACE GET_DEVICE_DATA_RDONLY

INTERFACE GET_DEVICE_DATA_RDWR
#:for dtype, suffix, rank, default in field_descriptors
#:set field_type_name = 'FIELD_%s%sD' % (suffix, rank)
  MODULE PROCEDURE :: ${field_type_name}$_GET_DEVICE_DATA_RDWR
#:endfor
END INTERFACE GET_DEVICE_DATA_RDWR

INTERFACE GET_HOST_DATA_RDONLY
#:for dtype, suffix, rank, default in field_descriptors
#:set field_type_name = 'FIELD_%s%sD' % (suffix, rank)
  MODULE PROCEDURE :: ${field_type_name}$_GET_HOST_DATA_RDONLY
#:endfor
END INTERFACE GET_HOST_DATA_RDONLY

INTERFACE GET_HOST_DATA_RDWR
#:for dtype, suffix, rank, default in field_descriptors
#:set field_type_name = 'FIELD_%s%sD' % (suffix, rank)
  MODULE PROCEDURE :: ${field_type_name}$_GET_HOST_DATA_RDWR
#:endfor
END INTERFACE GET_HOST_DATA_RDWR

CONTAINS

#:for dtype, suffix in zip(dtypes, suffixes)
  SUBROUTINE FILL_BUFFER_${suffix}$2D(BUFFER, INDEX)
    ! Utility routine to fill data buffers (views)
    ${dtype}$, POINTER, INTENT(INOUT) :: BUFFER(:)
    INTEGER(KIND=JPIM), INTENT(IN) :: INDEX
    INTEGER(KIND=JPIM) :: IDX

    IDX = INDEX+1
    BUFFER(IDX:) = BUFFER(INDEX)
  END SUBROUTINE FILL_BUFFER_${suffix}$2D

  SUBROUTINE FILL_BUFFER_${suffix}$3D(BUFFER, INDEX)
    ! Utility routine to fill data buffers (views)
    ${dtype}$, POINTER, INTENT(INOUT) :: BUFFER(:,:)
    INTEGER(KIND=JPIM), INTENT(IN) :: INDEX
    INTEGER(KIND=JPIM) :: I, IDX

    IDX = INDEX+1
    DO I=1, SIZE(BUFFER, 2)
      BUFFER(IDX:,I) = BUFFER(INDEX,I)
    END DO
  END SUBROUTINE FILL_BUFFER_${suffix}$3D

  SUBROUTINE FILL_BUFFER_${suffix}$4D(BUFFER, INDEX)
    ! Utility routine to fill data buffers (views)
    ${dtype}$, POINTER, INTENT(INOUT) :: BUFFER(:,:,:)
    INTEGER(KIND=JPIM), INTENT(IN) :: INDEX
    INTEGER(KIND=JPIM) :: I, J, IDX

    IDX = INDEX+1
    DO I=1, SIZE(BUFFER, 2)
      DO J=1, SIZE(BUFFER, 3)
        BUFFER(IDX:,I,J) = BUFFER(INDEX,I,J)
      END DO
    END DO
  END SUBROUTINE FILL_BUFFER_${suffix}$4D

  SUBROUTINE FILL_BUFFER_${suffix}$5D(BUFFER, INDEX)
    ! Utility routine to fill data buffers (views)
    ${dtype}$, POINTER, INTENT(INOUT) :: BUFFER(:,:,:,:)
    INTEGER(KIND=JPIM), INTENT(IN) :: INDEX
    INTEGER(KIND=JPIM) :: I, J, K, IDX

    IDX = INDEX+1
    DO I=1, SIZE(BUFFER, 2)
      DO J=1, SIZE(BUFFER, 3)
        DO K=1, SIZE(BUFFER, 4)
          BUFFER(IDX:,I,J,K) = BUFFER(INDEX,I,J,K)
        END DO
      END DO
    END DO
  END SUBROUTINE FILL_BUFFER_${suffix}$5D

#:endfor

#:for dtype, suffix, rank, default in field_descriptors
#:set field_type_name = 'FIELD_%s%sD' % (suffix, rank)
  FUNCTION ${field_type_name}$_EMPTY(SHAPE) RESULT(SELF)
    ! Create FIELD object by wrapping existing data
    !
    ! If a SHAPE is provided, a single empty buffer block-sized buffer
    ! will be allocated under %VIEW and used by all threads in a
    ! thread-parallel region to avoid segfault when dereferencing NULL
    ! pointers. Otherwise %DATA and %VIEW will always be unassociated.
    TYPE(${field_type_name}$) :: SELF
    INTEGER(KIND=JPIM), OPTIONAL, INTENT(IN) :: SHAPE(${rank-1}$)

    SELF%PTR => NULL()
    SELF%ACTIVE = .FALSE.
    SELF%THREAD_BUFFER = .FALSE.
    SELF%NBLOCKS = 0
  END FUNCTION ${field_type_name}$_EMPTY

#:endfor

#:for dtype, suffix, rank, default in field_descriptors
#:set field_type_name = 'FIELD_%s%sD' % (suffix, rank)
  FUNCTION ${field_type_name}$_WRAP(DATA, PERSISTENT, LBOUNDS) RESULT(SELF)
    ! Create FIELD object by wrapping existing data
    TYPE(${field_type_name}$_WRAPPER), TARGET :: SELF
    ${dtype}$, TARGET, INTENT(IN) :: DATA(${shapes[rank]}$)
    LOGICAL, INTENT(IN), OPTIONAL :: PERSISTENT
    INTEGER(KIND=JPIM), INTENT(IN), OPTIONAL :: LBOUNDS(${rank}$)
    LOGICAL :: LLPERSISTENT

    LLPERSISTENT = .TRUE.
    IF (PRESENT (PERSISTENT)) LLPERSISTENT = PERSISTENT
    IF (PRESENT (LBOUNDS)) SELF%LBOUNDS = LBOUNDS

    SELF%PTR => DATA
    SELF%ACTIVE = .TRUE.
    SELF%THREAD_BUFFER = .NOT. LLPERSISTENT
    SELF%NBLOCKS = SIZE(SELF%PTR, ${rank}$)

    SELF%ISTATUS = NHSTFRESH

    IF (.NOT. LLPERSISTENT) THEN
      IF (OML_MAX_THREADS() /= SIZE (DATA, ${rank}$)) THEN
        CALL ABOR1 ('${field_type_name}$_WRAP: DIMENSION MISMATCH')
      ENDIF
    ENDIF

  END FUNCTION ${field_type_name}$_WRAP

#:endfor

#:for dtype, suffix, rank, default in field_descriptors
#:set field_type_name = 'FIELD_%s%sD' % (suffix, rank)
  FUNCTION ${field_type_name}$_WRAP_PACKED(DATA, IDX, LBOUNDS) RESULT(SELF)
    ! Create FIELD object packed in a multi-field buffer by storing a
    ! contiguous pointer to existing data and an index.
    TYPE(${field_type_name}$_WRAPPER), TARGET :: SELF
    ${dtype}$, TARGET, INTENT(IN) :: DATA(${shapes[rank+1]}$)
    INTEGER(KIND=JPIM), INTENT(IN) :: IDX
    INTEGER(KIND=JPIM), INTENT(IN), OPTIONAL :: LBOUNDS(${rank}$)

    IF (PRESENT (LBOUNDS)) SELF%LBOUNDS = LBOUNDS

    SELF%PTR => DATA(${shapes[rank-1]}$,IDX,:)
    SELF%ACTIVE = .TRUE.
    SELF%THREAD_BUFFER = .FALSE.
    SELF%NBLOCKS = SIZE(SELF%PTR, ${rank}$)
  END FUNCTION ${field_type_name}$_WRAP_PACKED

#:endfor

#:for dtype, suffix, rank, default in field_descriptors
#:set field_type_name = 'FIELD_%s%sD' % (suffix, rank)
  SUBROUTINE ALLOCATE_${field_type_name}$ (SELF, SHAPE, NBLOCKS, PERSISTENT, LBOUNDS) 
    ! Create FIELD object by explicitly allocating new data
    !
    ! Please note that SHAPE is the conceptual shape without the block dimension
    CLASS(${field_type_name}$_OWNER), TARGET :: SELF
    INTEGER(KIND=JPIM), INTENT(IN) :: SHAPE(${rank-1}$)
    INTEGER(KIND=JPIM), INTENT(IN), OPTIONAL :: NBLOCKS
    LOGICAL, INTENT(IN), OPTIONAL :: PERSISTENT
    INTEGER(KIND=JPIM), INTENT(IN), OPTIONAL :: LBOUNDS(${rank}$)
    INTEGER(KIND=JPIM) :: NBLK

    IF (PRESENT (LBOUNDS)) SELF%LBOUNDS = LBOUNDS

    ! By default we allocate thread-local temporaries
    SELF%THREAD_BUFFER = .TRUE.
    NBLK = OML_MAX_THREADS()

    IF (PRESENT(PERSISTENT)) THEN
      IF (PERSISTENT) THEN
        ! Adjust outer dim for full-sized persistent blocked arrays
        IF (.NOT. PRESENT(NBLOCKS)) CALL &
         & ABOR1('${field_type_name}$_ALLOCATE : NBLOCKS not given for persistent allocation!')
        SELF%THREAD_BUFFER = .FALSE.
        NBLK = NBLOCKS
      END IF
    END IF

    ! Allocate storage array and store metadata
    ALLOCATE(SELF%PTR(${','.join('SHAPE(%d)'%(r+1) for r in range(rank-1))}$,NBLK))
    SELF%ACTIVE = .TRUE.
    SELF%NBLOCKS = SIZE(SELF%PTR, ${rank}$)
  END SUBROUTINE ALLOCATE_${field_type_name}$

  FUNCTION ${field_type_name}$_ALLOCATE(SHAPE, NBLOCKS, PERSISTENT, LBOUNDS) RESULT(SELF)
    ! WARNING : Using this function kills performance when the %DATA member is allocated
    TYPE(${field_type_name}$_OWNER), TARGET :: SELF
    INTEGER(KIND=JPIM), INTENT(IN) :: SHAPE(${rank-1}$)
    INTEGER(KIND=JPIM), INTENT(IN), OPTIONAL :: NBLOCKS
    LOGICAL, INTENT(IN), OPTIONAL :: PERSISTENT
    INTEGER(KIND=JPIM), INTENT(IN), OPTIONAL :: LBOUNDS(${rank}$)
    INTEGER(KIND=JPIM) :: NBLK
   
    CALL ALLOCATE_${field_type_name}$ (SELF, SHAPE, NBLOCKS, PERSISTENT, LBOUNDS)

  END FUNCTION ${field_type_name}$_ALLOCATE

#:endfor

#:for dtype, suffix, rank, default in field_descriptors
#:set field_type_name = 'FIELD_%s%sD' % (suffix, rank)
  FUNCTION ${field_type_name}$_GET_VIEW(SELF, BLOCK_INDEX, BLOCK_SIZE, ZERO) RESULT(VIEW_PTR)
    ! Updates internal view and exports it to an external pointer
    CLASS(${field_type_name}$), TARGET :: SELF
    ${dtype}$, POINTER :: VIEW_PTR(${shapes[rank-1]}$)
    INTEGER(KIND=JPIM), INTENT(IN) :: BLOCK_INDEX
    INTEGER(KIND=JPIM), INTENT(IN), OPTIONAL :: BLOCK_SIZE
    LOGICAL, OPTIONAL, INTENT(IN) :: ZERO
    INTEGER(KIND=JPIM) :: IDX

    IDX = BLOCK_INDEX
    IF (SELF%THREAD_BUFFER) IDX = OML_MY_THREAD()
    IF (SELF%ACTIVE) THEN
      VIEW_PTR (${lbptr (rank-1)}$) => SELF%PTR(${','.join(':' for _ in range(rank-1))}$,IDX)
    ELSE
      VIEW_PTR (${lbptr (rank-1)}$) => NULL()
    END IF

    IF (PRESENT(BLOCK_SIZE)) THEN
      IF (BLOCK_INDEX == SELF%NBLOCKS) THEN
        ! Fill the the buffer by replicating the last entry
        CALL FILL_BUFFER(VIEW_PTR, INDEX=BLOCK_SIZE)
      END IF
    END IF

    IF (PRESENT(ZERO)) THEN
      IF (ZERO) VIEW_PTR(${shapes[rank-1]}$) = ${default}$
    END IF
  END FUNCTION ${field_type_name}$_GET_VIEW

#:endfor

#:for dtype, suffix, rank, default in field_descriptors
#:set field_type_name = 'FIELD_%s%sD' % (suffix, rank)
  SUBROUTINE ${field_type_name}$_CREATE_DEVICE(SELF)
    ! Initialize a copy of this field on GPU device
    CLASS(${field_type_name}$), TARGET :: SELF

    SELF%DEVPTR => SELF%PTR
!$acc enter data create(SELF%PTR)
  END SUBROUTINE ${field_type_name}$_CREATE_DEVICE

#:endfor

#:for dtype, suffix, rank, default in field_descriptors
#:set field_type_name = 'FIELD_%s%sD' % (suffix, rank)
  FUNCTION ${field_type_name}$_WRAPPER_GET_DEVICE_DATA(SELF) RESULT(DEVPTR)
    ! Initialize a copy of this field on GPU device
    CLASS(${field_type_name}$_WRAPPER), TARGET :: SELF
    ${dtype}$, POINTER, CONTIGUOUS :: DEVPTR(${shapes[rank]}$)

    DEVPTR (${lbptr (rank)}$) => SELF%DEVPTR
  END FUNCTION ${field_type_name}$_WRAPPER_GET_DEVICE_DATA

  FUNCTION ${field_type_name}$_OWNER_GET_DEVICE_DATA(SELF) RESULT(DEVPTR)
    ! Initialize a copy of this field on GPU device
    CLASS(${field_type_name}$_OWNER), TARGET :: SELF
    ${dtype}$, POINTER, CONTIGUOUS :: DEVPTR(${shapes[rank]}$)

    DEVPTR (${lbptr (rank)}$) => SELF%PTR
  END FUNCTION ${field_type_name}$_OWNER_GET_DEVICE_DATA

#:endfor

#:for dtype, suffix, rank, default in field_descriptors
#:set field_type_name = 'FIELD_%s%sD' % (suffix, rank)
  SUBROUTINE ${field_type_name}$_WRAPPER_UPDATE_DEVICE(SELF)
    ! Create a copy of this field on device and copy data over
    CLASS(${field_type_name}$_WRAPPER), TARGET :: SELF

!$acc enter data copyin(SELF%PTR)
    SELF%DEVPTR => SELF%PTR
  END SUBROUTINE ${field_type_name}$_WRAPPER_UPDATE_DEVICE

  SUBROUTINE ${field_type_name}$_OWNER_UPDATE_DEVICE(SELF)
    ! Create a copy of this field on device and copy data over
    CLASS(${field_type_name}$_OWNER), TARGET :: SELF

    ALLOCATE(SELF%DEVPTR, SOURCE=SELF%PTR)
!$acc enter data copyin(SELF%DEVPTR)
  END SUBROUTINE ${field_type_name}$_OWNER_UPDATE_DEVICE

#:endfor

#:for dtype, suffix, rank, default in field_descriptors
#:set field_type_name = 'FIELD_%s%sD' % (suffix, rank)
  SUBROUTINE ${field_type_name}$_WRAPPER_UPDATE_HOST(SELF)
    ! Synchronize device data back to host
    CLASS(${field_type_name}$_WRAPPER) :: SELF

    !$acc exit data copyout(SELF%DEVPTR)
    SELF%PTR(${shapes[rank]}$) = SELF%DEVPTR(${shapes[rank]}$)
  END SUBROUTINE ${field_type_name}$_WRAPPER_UPDATE_HOST

  SUBROUTINE ${field_type_name}$_OWNER_UPDATE_HOST(SELF)
    ! Synchronize device data back to host
    CLASS(${field_type_name}$_OWNER) :: SELF

    !$acc exit data copyout(SELF%PTR)
  END SUBROUTINE ${field_type_name}$_OWNER_UPDATE_HOST

#:endfor

#:for dtype, suffix, rank, default in field_descriptors
#:set field_type_name = 'FIELD_%s%sD' % (suffix, rank)
  SUBROUTINE ${field_type_name}$_WRAPPER_DELETE_DEVICE(SELF)
    ! Initialize a copy of this field on GPU device
    CLASS(${field_type_name}$_WRAPPER), TARGET :: SELF

!$acc exit data delete(SELF%DEVPTR)
    DEALLOCATE(SELF%DEVPTR)
  END SUBROUTINE ${field_type_name}$_WRAPPER_DELETE_DEVICE

  SUBROUTINE ${field_type_name}$_OWNER_DELETE_DEVICE(SELF)
    ! Initialize a copy of this field on GPU device
    CLASS(${field_type_name}$_OWNER), TARGET :: SELF

!$acc exit data delete(SELF%DEVPTR)
    NULLIFY(SELF%DEVPTR)
  END SUBROUTINE ${field_type_name}$_OWNER_DELETE_DEVICE

#:endfor

#:for dtype, suffix, rank, default in field_descriptors
#:set field_type_name = 'FIELD_%s%sD' % (suffix, rank)
  SUBROUTINE ${field_type_name}$_FINAL(SELF)
    ! Finalizes field and dealloactes owned data
    CLASS(${field_type_name}$) :: SELF
    NULLIFY(SELF%PTR)
    IF (ASSOCIATED (SELF%DEVPTR)) THEN
      !$acc exit data delete (SELF%DEVPTR)
      DEALLOCATE (SELF%DEVPTR)
      NULLIFY (SELF%DEVPTR)
    ENDIF
  END SUBROUTINE ${field_type_name}$_FINAL

  SUBROUTINE ${field_type_name}$_WRAPPER_FINAL(SELF)
    ! Finalizes field and dealloactes owned data
    CLASS(${field_type_name}$_WRAPPER) :: SELF
    CALL SELF%${field_type_name}$%FINAL
  END SUBROUTINE ${field_type_name}$_WRAPPER_FINAL

  SUBROUTINE ${field_type_name}$_OWNER_FINAL(SELF)
    ! Finalizes field and dealloactes owned data
    CLASS(${field_type_name}$_OWNER) :: SELF
    IF (ALLOCATED(SELF%PTR)) THEN
	    DEALLOCATE(SELF%PTR)
    END IF
    CALL SELF%${field_type_name}$%FINAL
  END SUBROUTINE ${field_type_name}$_OWNER_FINAL

#:endfor

#:for dtype, suffix, rank, default in field_descriptors
#:set field_type_name = 'FIELD_%s%sD' % (suffix, rank)
  INTEGER (KIND=JPIM) FUNCTION ${field_type_name}$_GET_LAST_CONTIGUOUS_DIMENSION (PTR) RESULT (JDIM)
  ${dtype}$, POINTER :: PTR (${shapes[rank]}$)
  INTEGER*8 :: ISTRIDE (${rank}$)
  INTEGER (KIND=JPIM) :: J

  ISTRIDE (1) = KIND (PTR)
  DO J = 2, ${rank}$
    ISTRIDE (J) = ISTRIDE (J-1) * SIZE (PTR, J-1) 
  ENDDO

  JDIM = 0
#:for d in range (1, rank+1)
  #:set ind0 = ['1'] * rank
  #:set ind1 = ['1'] * (d - 1) + ['2'] + ['1'] * (rank - d)
  IF (LOC (PTR (${', '.join (ind1)}$)) - LOC (PTR (${', '.join (ind0)}$)) /= ISTRIDE (${d}$)) THEN
    RETURN
  ENDIF

  JDIM = ${d}$

#:endfor
  END FUNCTION ${field_type_name}$_GET_LAST_CONTIGUOUS_DIMENSION

  SUBROUTINE ${field_type_name}$_COPY (HST, DEV, KDIR)
  ${dtype}$, POINTER :: HST (${shapes[rank]}$), DEV (${shapes[rank]}$)
  INTEGER (KIND=JPIM), INTENT (IN) :: KDIR
  INTEGER (KIND=JPIM) :: JDIM

  JDIM = ${field_type_name}$_GET_LAST_CONTIGUOUS_DIMENSION (HST)

  SELECT CASE (JDIM)
#:for d in range (rank + 1)
    CASE (${d}$)
      CALL COPY_DIM${d}$_CONTIGUOUS (HST, DEV, KDIR)
#:endfor
  END SELECT

  CONTAINS

#:for d in range (0, rank+1)
    SUBROUTINE COPY_DIM${d}$_CONTIGUOUS (HST, DEV, KDIR)
    USE OPENACC
    ${dtype}$, POINTER :: HST (${shapes[rank]}$), DEV (${shapes[rank]}$)
    INTEGER (KIND=JPIM), INTENT (IN) :: KDIR
    INTEGER (KIND=JPIM) :: ISIZE
    INTEGER :: ${', '.join (['J'] + list (map (lambda i: 'J' + str (i+1), range (d, rank))))}$

  #:for e in range (rank, d, -1)
    ${'  ' * (rank - e)}$DO J${e}$ = LBOUND (HST, ${e}$), UBOUND (HST, ${e}$)
  #:endfor
  #:set ar = ', '.join ([':'] * d  + list (map (lambda i: 'J' + str (i+1), range (d, rank))))
  #:set indent = '  ' * (rank - e)
    #:if d == 0
    ${indent}$  ISIZE = KIND (HST)
    #:else
    ${indent}$  ISIZE = KIND (HST) * SIZE (HST (${ar}$))
    #:endif
    ${indent}$  IF (KDIR == NH2D) THEN
    ${indent}$    !$acc host_data use_device (DEV)
    ${indent}$    CALL ACC_MEMCPY_TO_DEVICE (DEV (${ar}$), HST (${ar}$), ISIZE)
    ${indent}$    !$acc end host_data
    ${indent}$  ELSEIF (KDIR == ND2H) THEN
    ${indent}$    !$acc host_data use_device (DEV)
    ${indent}$    CALL ACC_MEMCPY_FROM_DEVICE (HST (${ar}$), DEV (${ar}$), ISIZE)
    ${indent}$    !$acc end host_data
    ${indent}$  ENDIF
  #:for e in range (d, rank)
    ${'  ' * (rank - e - 1)}$ENDDO
  #:endfor
    END SUBROUTINE

#:endfor
  END SUBROUTINE ${field_type_name}$_COPY


  FUNCTION ${field_type_name}$_GET_HOST_DATA_ (SELF, MODE) RESULT (PTR)
    CLASS(${field_type_name}$), POINTER, INTENT (INOUT) :: SELF
    INTEGER (KIND=JPIM),                INTENT (IN) :: MODE
    ${dtype}$, TARGET, SAVE :: ZDUM (${ ', '.join ([dumsize] * (rank-1) + ['1']) }$)
    ${dtype}$, POINTER :: PTR(${shapes[rank]}$)

    IF (ASSOCIATED (SELF)) THEN
      IF (IAND (SELF%ISTATUS, NHSTFRESH) == 0) THEN
        CALL ${field_type_name}$_COPY (SELF%PTR, SELF%DEVPTR, ND2H)
        SELF%ISTATUS = IOR (SELF%ISTATUS, NHSTFRESH)
      ENDIF
      PTR (${lbptr (rank)}$) => SELF%PTR (${','.join(':' for _ in range(rank))}$)
      IF (IAND (MODE, NWR) /= 0) THEN
        SELF%ISTATUS = IAND (SELF%ISTATUS, NOT (NDEVFRESH))
      ENDIF
    ELSE
      PTR => ZDUM
    ENDIF

  END FUNCTION ${field_type_name}$_GET_HOST_DATA_

  FUNCTION ${field_type_name}$_GET_HOST_DATA_RDONLY (SELF) RESULT (PTR)
    CLASS(${field_type_name}$), POINTER, INTENT (INOUT) :: SELF
    ${dtype}$, POINTER :: PTR(${shapes[rank]}$)

    PTR => ${field_type_name}$_GET_HOST_DATA_ (SELF, NRD)

  END FUNCTION ${field_type_name}$_GET_HOST_DATA_RDONLY

  FUNCTION ${field_type_name}$_GET_HOST_DATA_RDWR (SELF) RESULT (PTR)
    CLASS(${field_type_name}$), POINTER, INTENT (INOUT) :: SELF
    ${dtype}$, POINTER :: PTR(${shapes[rank]}$)

    PTR => ${field_type_name}$_GET_HOST_DATA_ (SELF, IOR (NRD, NWR))

  END FUNCTION ${field_type_name}$_GET_HOST_DATA_RDWR

  FUNCTION ${field_type_name}$_GET_DEVICE_DATA_ (SELF, MODE) RESULT(PTR)
    CLASS(${field_type_name}$), POINTER, INTENT (INOUT) :: SELF
    INTEGER (KIND=JPIM),                INTENT (IN) :: MODE
    ${dtype}$, TARGET, SAVE :: ZDUM (${ ', '.join ([dumsize] * (rank-1) + ['1']) }$)
    ${dtype}$, POINTER :: PTR(${shapes[rank]}$)

    IF (ASSOCIATED (SELF)) THEN
      IF (IAND (SELF%ISTATUS, NDEVFRESH) == 0) THEN
        IF (.NOT. ASSOCIATED (SELF%DEVPTR)) THEN
          ALLOCATE (SELF%DEVPTR, MOLD=SELF%PTR)
          !$acc enter data create (SELF%DEVPTR)
        ENDIF
        CALL ${field_type_name}$_COPY (SELF%PTR, SELF%DEVPTR, NH2D)
        SELF%ISTATUS = IOR (SELF%ISTATUS, NDEVFRESH)
      ENDIF
      PTR (${lbptr (rank)}$) => SELF%DEVPTR (${','.join(':' for _ in range(rank))}$)
      IF (IAND (MODE, NWR) /= 0) THEN
        SELF%ISTATUS = IAND (SELF%ISTATUS, NOT (NHSTFRESH))
      ENDIF
    ELSE
      PTR => ZDUM
    ENDIF

  END FUNCTION ${field_type_name}$_GET_DEVICE_DATA_

  FUNCTION ${field_type_name}$_GET_DEVICE_DATA_RDONLY (SELF) RESULT (PTR)
    CLASS(${field_type_name}$), POINTER, INTENT (INOUT) :: SELF
    ${dtype}$, POINTER :: PTR(${shapes[rank]}$)

    PTR => ${field_type_name}$_GET_DEVICE_DATA_ (SELF, NRD)

  END FUNCTION ${field_type_name}$_GET_DEVICE_DATA_RDONLY

  FUNCTION ${field_type_name}$_GET_DEVICE_DATA_RDWR (SELF) RESULT (PTR)
    CLASS(${field_type_name}$), POINTER, INTENT (INOUT) :: SELF
    ${dtype}$, POINTER :: PTR(${shapes[rank]}$)

    PTR => ${field_type_name}$_GET_DEVICE_DATA_ (SELF, IOR (NRD, NWR))

  END FUNCTION ${field_type_name}$_GET_DEVICE_DATA_RDWR

#:endfor

END MODULE FIELD_MODULE
